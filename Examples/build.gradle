apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: 'generate-token.gradle'

apply from: "../gradle/ktlint.gradle"

def envVarOrLocalProperty(String name) {
    if (System.getenv(name) != null) {
        return System.getenv(name)
    }

    Properties properties = new Properties()
    def file = project.rootProject.file('local.properties')
    if (file.exists()) {
        properties.load(file.newDataInputStream())
        def localProperty = properties.getProperty(name)
        if (localProperty != null) {
            return localProperty
        }
    }
    return null
}

if (file("${rootDir}/gradle/versions.gradle").exists()) {
    apply from: "${rootDir}/gradle/versions.gradle"
}

android {
    compileSdkVersion 28

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    defaultConfig {
        applicationId "com.mapbox.vision.examples"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/java'
            java.srcDirs += 'src/main/kotlin'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:$android_x_appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$android_x_constraint_layout"

    implementation "com.mapbox.mapboxsdk:mapbox-android-sdk:$mapbox_sdk"
    implementation "com.mapbox.mapboxsdk:mapbox-android-navigation:$mapbox_navigation"
    implementation "com.mapbox.mapboxsdk:mapbox-android-navigation-ui:$mapbox_navigation"
    implementation "com.mapbox.mapboxsdk:mapbox-android-plugin-locationlayer:$mapbox_location_layer"
    implementation "com.mapbox.mapboxsdk:mapbox-android-core:$mapbox_core"

    ktlint "com.pinterest:ktlint:$ktlint_version"

    if (envVarOrLocalProperty('BUILD_CORE_FROM_SOURCE')?.toBoolean()) {
        implementation project(path: ':Vision')
        implementation project(path: ':VisionAr')
        implementation project(path: ':VisionSafety')
        implementation project(path: ':CoreAr')
        implementation project(path: ':CoreSdk')
        implementation project(path: ':CoreSafety')
    } else {
        implementation "com.mapbox.vision:mapbox-android-vision:$vision"
        implementation "com.mapbox.vision:mapbox-android-vision-ar:$vision_ar"
        implementation "com.mapbox.vision:mapbox-android-vision-safety:$vision_safety"
        implementation "com.mapbox.vision:mapbox-android-vision-safety-native:$vision_safety"
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
